/*--------------------------------------------------------------------------
/*            JOINCOVERS - Geomatics Senior Project - University of Florida
/*--------------------------------------------------------------------------
/*   Program: FILE_INIT.AML
/*   Purpose: Write a scratchfile and return full path.
/*
/*--------------------------------------------------------------------------
/*     Usage: FILE_INIT <variable_name> {prefix} {suffix}
/*
/* Arguments: variable_name - global variable to hold file path and name
/*                   prefix - prefix of the scratchfile
/*                   suffix - suffix of the scratchfile
/*
/*  Routines:  CHECK_OPENSTAT - check that file was opened without error.
/*            CHECK_CLOSESTAT - check that file was closed without error.
/*                      USAGE - return tool usage.
/*                       EXIT - cleanup and exit tool.
/*                    BAILOUT - error handling.
/*
/*   Globals: 
/*--------------------------------------------------------------------------
/*     Calls: 
/*--------------------------------------------------------------------------
/*     Notes: 
/*--------------------------------------------------------------------------
/*   History: Sam Henderson -  Nov 98 - Feb 99  - Original coding 
/*==========================================================================
/*
&args var prefix suffix
/*
&severity &error &routine bailout
/*
/* Check arguments 
&if [null %var%] &then
  &call usage
&if [null %prefix%] &then 
  &s prefix = xx

/* Write the scratchfile.
&if not [null %suffix%] &then 
  &s %var% [scratchname -prefix %prefix% -suffix %suffix% -file -full]
&else
  &s %var% [scratchname -prefix %prefix% -file -full]
&s [before %AML$FILE% .aml]unit = [open [value %var%] openstat -write]
&call CHECK_OPENSTAT
&s closestat = [close [value [before %AML$FILE% .aml]unit]]
&call CHECK_CLOSESTAT

&call EXIT

&return

/*---------------------
&routine CHECK_OPENSTAT
/*---------------------
/* Check status variable for open function.
&select %openstat%
  &when 100 
    &do
      &type No free AML units.
      &call BAILOUT
    &end
  &when 101 
    &do
      &type File not found.
      &call BAILOUT
    &end
  &when 102 
    &do
      &type Couldn't open file.
      &call BAILOUT
    &end
  &when 103
    &do
      &type Couldn't create file.
      &call BAILOUT
    &end
&end
&return

/*----------------------
&routine CHECK_CLOSESTAT
/*----------------------
/* Check that a file was closed
&if %closestat% ne 0 &then
  &type FILE CLOSE ERROR %closestat%.
&return

/*------------
&routine USAGE
/*------------
/* Display usage for this tool
&type Usage: FILE_INIT <variable_name> {prefix} {suffix}
&return &inform 

/*-----------
&routine EXIT
/*-----------
/* Clean up and exit menu
&dv .[before %AML$FILE% .aml]$*
&do file &list [listfile xx*[before %AML$FILE% .aml] -file]
  &s delstat = [delete %file%]
&end
&return
 
/*--------------
&routine BAILOUT
/*--------------
&severity &error &ignore
&call exit /* Comment out later?
&return &warning An error has occurred in [before %AML$FILE% .aml].

