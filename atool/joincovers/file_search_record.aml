/*--------------------------------------------------------------------------
/*            JOINCOVERS - Geomatics Senior Project - University of Florida
/*--------------------------------------------------------------------------
/*   Program: FILE_SEARCH_RECORD.AML
/*   Purpose: Search for a record in a file.  Set variable to .true. if 
/*            found, .false. if not. Alternatively, search for a string
/*            contained in any record.
/*
/*--------------------------------------------------------------------------
/*     Usage: FILE_SEARCH_RECORD INIT <file> <string> <variable_name>
/*                                    {EQUAL | CN}
/*
/* Arguments:         file  - the file to search 
/*                   string - the record or string to search for
/*            variable_name - variable = true -> record exists
/*                            variable = false -> record not found
/*                    EQUAL - the string must match the record
/*                       CN - if the string is contained in a record,
/*                            .true. will be returned
/* 
/*  Routines:    INIT - check args, set up loop structure
/*               LOOP - called by file_loop_aml to query current record
/*              USAGE - return tool usage.
/*               EXIT - cleanup and exit tool.
/*            BAILOUT - error handling.
/*  
/*   Globals: .file_search_record$*
/*--------------------------------------------------------------------------
/*     Calls: file_loop_aml.aml
/*--------------------------------------------------------------------------
/*     Notes:
/*--------------------------------------------------------------------------
/*   History: Sam Henderson -  Nov 98 - Feb 99  - Original coding 
/*==========================================================================
/*
&args routine arglist:rest
/*
&severity &error &routine bailout
/* Check arguments
&if ^ [null %routine%] &then
  &call %routine%
&else
  &call usage
&return


/*-----------
&routine INIT
/*-----------

/* Extract arguments
&s file = [extract 1 [unquote %arglist%]]
&s .file_search_record$searchrec = [extract 2 [unquote %arglist%]]
&s .file_search_record$var = [extract 3 [unquote %arglist%]]
&s .file_search_record$type = [extract 4 [unquote %arglist%]]

/* Check arguments
&if [null %file%] or [null %.file_search_record$searchrec%] or ~
 [null %.file_search_record$var%] &then
  &call usage
&if not [null %.file_search_record$type%] &then &do
  &if [quote [upcase %.file_search_record$type%]] ne 'EQUAL' and ~
      [quote [upcase %.file_search_record$type%]] ne 'CN' and ~
      [quote %.file_search_record$type%] ne '#' &then
    &call USAGE
&end
&else
  &s .file_search_record$type = EQUAL

/* Make sure file list exists.
&if not [exists %file% -file] &then
  &return Input file %file% not found.

&s %.file_search_record$var% = .false.
&run file_loop_aml %file% [quote file_search_record LOOP] ~
 .file_search_record$record

&call EXIT

&return

/*-----------
&routine LOOP
/*-----------
/* Check if current record matches search record.
&if [quote [upcase %.file_search_record$type%]] = 'CN' &then &do
  &if %.file_search_record$record% cn %.file_search_record$searchrec% &then
    &s %.file_search_record$var% .true.
&end
&else &do
  &if %.file_search_record$record% = %.file_search_record$searchrec% &then
    &s %.file_search_record$var% .true.
&end
&return


/*------------
&routine USAGE
/*------------
/* Display usage for this tool
&type Usage: FILE_SEARCH_RECORD INIT <file> <string> <variable_name> 
&type                           {EQUAL | CN}
&return &inform 

/*-----------
&routine EXIT
/*-----------
/* Clean up and exit menu
&dv .[before %AML$FILE% .aml]$*
&do file &list [listfile xx*[before %AML$FILE% .aml] -file]
  &s delstat = [delete %file%]
&end
&return
 
/*--------------
&routine BAILOUT
/*--------------
&severity &error &ignore
&call exit
&return &warning An error has occurred in [before %AML$FILE% .aml].

